template = _{ soi ~ (tag|literal)+ ~ eoi }

literal =  @{ (char | symbol | escape | quote | digit | " ")+ }

tag = _{ "<" ~ expan ~ ">" }
expan = _{ (property | bound_property | range | variable) }

// Property - bare-ident, optionally preceded by bindings.
property = { ident }
bound_property = { binding+ ~ ident }
binding = { "[" ~ ident ~ ":" ~ ident ~ "]" }
variable = { "$" ~ ident }
range = { "#" ~ number ~ "-" ~ number }


ident = @{ char ~ (char | digit | "_" )* }

escape     = _{ "\\" ~ (predefined | byte | unicode) }
predefined = _{ "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte       = _{ "x" ~ hex ~ hex }
unicode    = _{ "u" ~ "{" ~ unicode_hex ~ "}" }
quote = _{ "'" | "\"" }

unicode_hex = { hex ~ hex ~ hex ~ hex ~ hex ~ hex | hex+ }
hex = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }

symbol = _{ "!" | "?" | "," | "." | "_" | ":" | ";" | "-" }
char = _{ 'a'..'z' | 'A'..'Z' }
number = { digit+ }
digit = _{ '0'..'9' }
// _whitespace = _{ " " | "\r" | "\t" | "\n" }
